repositories {
    maven { url = "https://oss.sonatype.org/content/repositories/snapshots" }
    maven { url = "https://hub.spigotmc.org/nexus/content/groups/public/" }
    maven { url = "https://libraries.minecraft.net" }
}

configurations {
    create("shadeBlindly")
    create("testing")
    implementation.extendsFrom(testing)
}

dependencies {
    compileOnly(project(":common"))

    compileOnly("org.spigotmc:spigot-api:1.13.2-R0.1-SNAPSHOT")

    /* For adventure components support */
    compileOnly("net.kyori:adventure-platform-bukkit:4.1.1")

    /* For Brigadier on 1.13+ */
    compileOnly("com.mojang:brigadier:1.0.18")

    /*
     * We shade paper-internal without adding it as a dependency
     * because it requires Java 17. We have to access it reflectively though :(
     */
    shadeBlindly(project(":paper-internal-dont-use"))
}

compileJava {
    options.compilerArgs += ["-parameters"]
    options.fork = true
    options.forkOptions.executable = "javac"
}

jar {
    duplicatesStrategy = DuplicatesStrategy.EXCLUDE
    from configurations.testing.asFileTree.files.collect { zipTree(it) }
    from configurations.shadeBlindly.asFileTree.files.collect { zipTree(it) }
}
